!熱翹曲脫層分析VCCT後處理一2017/12/28
/post1
			*afun,rad
			pi=acos(-1)
			*afun,deg
rsys,0
plnsol,u,z
alls
!■■■■■■■■■■先算一個gap大小■■■■■■■■■■!
csys,100	$wpcsys,,100
nsel,s,,,crackdown_n
nsel,r,loc,x,length/2
nsel,r,loc,y,width/2
   *get,disd_n,node,,num,min	
   *get,disdown_uz,node,disd_n,u,z


nsel,s,,,crackup_n
nsel,r,loc,x,length/2
nsel,r,loc,y,width/2
   *get,disu_n,node,,num,min	
   *get,disup_uz,node,disu_n,u,z
   
crackgap=abs(disdown_uz-disup_uz)


!■■■■■■■■■■VCCT算一個GT大小■■■■■■■■■■!
alls

/prep7
alls
asum
alls

!!定義座標系
local,11,0,length-crack_R/2,width-crack_R/2,t%cracklayer%,-45		!切口裂紋中心(45度卡氏座標)
!local,21,0,length-crack_R,width-crack_R,t%cracklayer%				!試片裂紋高圓心點1(卡氏座標系)

!▼▼▼▼▼▼抓取裂紋節點資訊▼▼▼▼▼▼!

loc_2=
*dim,loc_2,array,crack_cut+1,4
csys,200	$wpcsys,,200
*do,i,1,crack_cut+1,1
x_node=kp1_matrix(i,2)
y_node=kp1_matrix(i,3)
nsel,s,loc,z,0
nsel,r,loc,x,x_node
nsel,r,loc,y,y_node
	*GET,num_node,node,,num,min
	*GET,z_node,NODE,num_node,loc,z
loc_2(I,1)=num_node			!節點編號
loc_2(I,2)=x_node			!loc_x
loc_2(I,3)=y_node			!loc_y
loc_2(I,4)=z_node			!loc_z
*enddo

loc_1=
*dim,loc_1,array,crack_cut+1,4
csys,200	$wpcsys,,200
*do,i,1,crack_cut+1,1
x_node=kp5_matrix(i,2)
y_node=kp5_matrix(i,3)
nsel,s,loc,z,0
nsel,r,loc,x,x_node
nsel,r,loc,y,y_node
	*GET,num_node,node,,num,min
	*GET,z_node,NODE,num_node,loc,z
loc_1(I,1)=num_node			!節點編號
loc_1(I,2)=x_node			!loc_x
loc_1(I,3)=y_node			!loc_y
loc_1(I,4)=z_node			!loc_z
*enddo

loc_3up=
*dim,loc_3up,array,crack_cut+1,4
csys,200	$wpcsys,,200
*do,i,1,crack_cut+1,1
x_node=kp4_matrix(i,2)
y_node=kp4_matrix(i,3)
nsel,s,loc,z,0
nsel,r,loc,x,x_node
nsel,r,loc,y,y_node
nsel,r,,,crackup_n
	*GET,num_node,node,,num,min
	*GET,z_node,NODE,num_node,loc,z
loc_3up(I,1)=num_node		!節點編號
loc_3up(I,2)=x_node			!loc_x
loc_3up(I,3)=y_node			!loc_y
loc_3up(I,4)=z_node			!loc_z
*enddo

loc_3down=
*dim,loc_3down,array,crack_cut+1,4
csys,200	$wpcsys,,200
*do,i,1,crack_cut+1,1
x_node=kp4_matrix(i,2)
y_node=kp4_matrix(i,3)
nsel,s,loc,z,0
nsel,r,loc,x,x_node
nsel,r,loc,y,y_node
nsel,r,,,crackdown_n
	*GET,num_node,node,,num,min
	*GET,z_node,NODE,num_node,loc,z
loc_3down(I,1)=num_node		!節點編號
loc_3down(I,2)=x_node			!loc_x
loc_3down(I,3)=y_node			!loc_y
loc_3down(I,4)=z_node			!loc_z
*enddo

!▲▲▲▲▲▲▲▲▲▲▲▲



/post1
!▼▼▼▼▼▼算GT▼▼▼▼▼▼!
rsys,solu
rsys,lsys
!set,last
alls
*DO,j,1,crack_cut,1
csys,100	$wpcsys,,100
	!裂紋開口位移
	nsel,s,,,loc_3up(j,1)
		*get,Nup,node,,num,min
		*get,Uxup,node,Nup,u,x
		*get,Uyup,node,Nup,u,y
		*get,Uzup,node,Nup,u,z
	nsel,s,,,loc_3down(j,1)
		*get,Ndown,node,,num,min
		*get,Uydown,node,Ndown,u,y 
		*get,Uxdown,node,Ndown,u,x
		*get,Uzdown,node,Ndown,u,z
	
	V1_1st=abs(Uxup-Uxdown)
	V2_1st=abs(Uyup-Uydown)
	V3_1st=abs(Uzup-Uzdown)
	
	nsel,s,,,loc_3up(j+1,1)
		*get,Nup,node,,num,min
		*get,Uxup,node,Nup,u,x
		*get,Uyup,node,Nup,u,y
		*get,Uzup,node,Nup,u,z
	nsel,s,,,loc_3down(j+1,1)
		*get,Ndown,node,,num,min
		*get,Uydown,node,Ndown,u,y 
		*get,Uxdown,node,Ndown,u,x
		*get,Uzdown,node,Ndown,u,z
	
	V1_2nd=abs(Uxup-Uxdown)
	V2_2nd=abs(Uyup-Uydown)
	V3_2nd=abs(Uzup-Uzdown)
	
!------	
	!節點力_1st
	csys,200	$wpcsys,,200
	eslv
	esel,u,mat,,1			!切開才能讀內應力

	nsel,s,,,loc_2(j,1)
	csys,100	$wpcsys,,100
	fsum
	*get,Nf,node,,num,min
	*get,P1_1st,fsum,,item,fx
	*get,P2_1st,fsum,,item,fy
	*get,P3_1st,fsum,,item,fz
	P1_1st=abs(P1_1st)
	P2_1st=abs(P2_1st)
	P3_1st=abs(P3_1st)	
	!算權重C_1st
	nsel,s,,,loc_2(j,1)
	csys,200	$wpcsys,,200
	$asel,s,loc,z,0        $asln,r
		*if,j,eq,1,then
			sj=0
		*else
			nsel,s,,,loc_2(j-1,1)
			asln,r
			nsel,s,,,loc_1(j,1)
			asln,r
			*get,aj,area,0,num,min
			*get,sj,area,aj,area	
		*endif
	
	nsel,s,,,loc_2(j,1)
	csys,200	$wpcsys,,200
	$asel,s,loc,z,0        $asln,r
			nsel,s,,,loc_2(j+1,1)
			asln,r
			nsel,s,,,loc_1(j,1)
			asln,r
			*get,aj2,area,0,num,min
			*get,sj2,area,aj2,area		

	c1=sj2/(sj+sj2)
	
	!---
	
	!節點力_2nd
	csys,200	$wpcsys,,200
	eslv
	esel,u,mat,,1			!切開才能讀內應力

	nsel,s,,,loc_2(j+1,1)
	csys,100	$wpcsys,,100
	fsum
	*get,Nf,node,,num,min
	*get,P1_2nd,fsum,,item,fx
	*get,P2_2nd,fsum,,item,fy
	*get,P3_2nd,fsum,,item,fz
	P1_2nd=abs(P1_2nd)
	P2_2nd=abs(P2_2nd)
	P3_2nd=abs(P3_2nd)	
	!算權重C_2nd
	nsel,s,,,loc_2(j+1,1)
	csys,200	$wpcsys,,200
	$asel,s,loc,z,0        $asln,r
			nsel,s,,,loc_2(j+1-1,1)
			asln,r
			nsel,s,,,loc_1(j+1,1)
			asln,r
			*get,aj,area,0,num,min
			*get,sj,area,aj,area		
		
	nsel,s,,,loc_2(j+1,1)
	csys,200	$wpcsys,,200
	$asel,s,loc,z,0        $asln,r
		*if,j,eq,crack_cut,then
			sj2=0
		*else
			nsel,s,,,loc_2(j+1+1,1)
			asln,r
			nsel,s,,,loc_1(j+1,1)
			asln,r			
			*get,aj2,area,0,num,min
			*get,sj2,area,aj2,area
		*endif
		
	c2=sj/(sj+sj2)
	
!------	
	nsel,s,,,loc_2(j,1)
	csys,200	$wpcsys,,200
	$asel,s,loc,z,0        $asln,r
		nsel,s,,,loc_2(j+1,1)
		asln,r
		nsel,s,,,loc_3up(j,1)
		asln,r
		*get,ajj,area,0,num,min
		*get,sjj,area,ajj,area

!g前面
gg88=1/(2*(sj-0.25*(sj-sjj)))

	*do,ii,1,3,1
	G%ii%_%j%=gg88*((C1*v%ii%_1st*p%ii%_1st)+(C2*v%ii%_2nd*p%ii%_2nd))
	*enddo
Gt_%j%=G1_%j%+G2_%j%+G3_%j%


!------
!找出元素z方向求deg
csys,11	$wpcsys,,11
deg_%j%=(1/crack_cut)*((j-1)+j)/2

!!找出元素z方向求deg
!deg_%j%=(loc_2(j,3)+loc_2(j+1,3))/2

alls
*enddo


GT_Matrix=
*dim,GT_Matrix,array,crack_cut,2
	*do,I,1,crack_cut,1
	GT_Matrix(i,1)=deg_%i%
	GT_Matrix(i,2)=Gt_%i%*1000
	*enddo
*mwrite,GT_Matrix,GT_Matrix,dat,,,,crack_cut,2
(5E15.5)


!■■■■■■■■■■脫層介面應力圖■■■■■■■■■■!
csys,100	$wpcsys,,100
nsel,s,loc,z,t1
esln	$esel,r,mat,,1
